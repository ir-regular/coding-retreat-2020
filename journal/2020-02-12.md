# Developer Journal, 12-14 February 2020

## Haskell: yet another monad tutorial

**You have to add `build-depends: mtl` to your `.cabal` file**

Pure functions. Function application (`$`). Function composition (`.`).

Monads:

- IO (streams, i/o)
- error (exceptions)
- State s (interacting with global or local state)
- Maybe (function failure)
- (list) (return multiple values in parallel; special support so it doesn't have a name)
- Reader (read only environment)

Monad: `f :: a -> m b` - a function from `a` to `b` in the `m` monad

Comonad: `f :: c a -> b` - a function from `a` to `b`, where `c` is a comonad

`m` == _type constructor_. For example, `Maybe`. It is a function on types,
mapping one input type to one output type. The type constructor Maybe has
the kind `* -> *`. A "kind" is a type of types. Primitive types have the kind
`*`, which just means that they aren't type functions (i.e. type constructors).

The unit type and unit value are both written as `()`.

Monads are not _really_ values, just written that way. It's as if values
of type `m b` are really functions of type `() -> m b`. That's why they're often
called "actions". (Sometimes we also talk about "performing an action" or
"executing an action" which is like applying the function.)

Normal composition doesn't work on monadic functions, because there is no
way to universally "extract" a value from a "monad value".

    Side note: there exists an extract function for IO monad,
    `unsafePerformIO` - there are legitimate uses for it (for instance, deep down
    in the implementation of Haskell compilers), but for general use it shouldn't
    be needed.

### Monad class

```haskell
class Monad m where
  (>>=)  :: m a -> (  a -> m b) -> m b
  (>>)   :: m a ->  m b         -> m b
  return ::   a                 -> m a
  fail   :: String -> m a
```

#### Monadic application, bind operator: `>>=`
    
There does exist a "bind" operator, which allows passing a "monadic value"
to a function which takes a "bare" value and converts it into a "monadic value"
in the same monad:

`(>>=) :: m a -> (a -> m b) -> m b`

Every monad has to define its own monadic apply operator, because for each
of them the "extraction" of value is different.

#### Monadic composition: `>=>`

(You have to import `Control.Monad` before using it)

```haskell
(>=>) :: Monad m => (a -> m b) -> (b -> m c) -> a -> m c
f >=> g = \x -> (f x >>= g)
```

#### Convert a function to monadic function: `return`

The unfortunately named function has signature `return :: a -> m a`

So if we wanted to compose a monadic function `f` with a normal function `g`,
we could write:

`h = f >=> (return . g)`

Type checker figures out what type the return should convert to. But if you
have to for some reason, you can annotate it: `(return 10 :: IO Int)`

Every monad has to define its own `return` function.

`return` is an identity for monadic function composition.
(And so as we can see, a monad is a monoid in the category of endofunctors.)

#### Bind, but drop the intermediate value: `>>`

When the first function returns something like `IO ()`, you want to be able
to ignore `()` rather than pass it to a follow-up function.

Which means you can do this:

`(putStrLn "This is string 1.") >> (putStrLn "This is string 2.")`

#### do notation

```haskell
-- instead of...
mv >>= \x -> mf x
-- you can write...
do v <- mv
   f v
-- so there is actually an "extract" notation eh

-- instead of...
mv >> mv2
-- you can write...
do mv
   mv2

-- And so fundamentally this is possible
do v1 <- mv
   v2 <- f v1
   mv3
   return v2
```

_GAH_

#### `fail` function

Default definition: `fail s = error s`

It is called when a pattern match fails (provided monadic function is not total).
For example the code below desugars to a case statement ending with `_ -> fail "..."`

```haskell
do (Foo i) <- return (Bar "xxx")
  return i
```

### "Monad laws"

"Easy" version - makes it explicit that a monad is a kind of monoid.
Monoid: has an operation (here: `>=>`) which is associative
and has an identity element (here `return`).

1. `return >=> f == f`
2. `f >=> return == f`
3. `(f >=> g) >=> h == f >=> (g >=> h)`

or alternatively (which is not so obvious)

1. `return x >>= f == fx`
2. `mv >>= return == mv`
3. `(mv >>= f) >>= g == mv >>=(\x -> (f x >>= g))`

Haskell does not enforce these laws, so it's up to the programmer who is
implementing the class to make sure they will hold.

> There is an extension to the `Monad` type class called `MonadPlus` which adds
> extra functionality to monads (specifically, it defines a "zero" element for
> a monad as well as a way of "adding" two monadic values.) (For instance, there
> is a generalization of the concat function called msum which works for any instance of
> `MonadPlus`, including lists.) 

### Haskell: error handling

Options available:

- The error function
- Exceptions in the IO monad
- Extensible exceptions
- Monadic exceptions

Use `error` function for errors that cannot be recovered from. It is there so
that you can write total functions (with defined behaviours for all possible
inputs) - while handling invalid inputs.

Haskell 98 standard: `IO` monad allows raising errors using `ioError` function
and catching them with `catch` function. The error is of a data type
`IOException` or `IOError`. It includes a field to indicate error condition type.
The values this field can take are limited. Use `UserError` as a catch-all.

GHC extension of the above: `Control.Exception` module. It allows you to add
your own exception types, as long as they are instances of `Exception` type
class. As above, they can only be caught in the `IO` monad.

A special-purpose error handling monad. This is the cleanest way, because
`IO` monad is overloaded with stuff that has been dumped into it over the years
which has nothing to do with actual I/O.

You can represent the error conditions with `Either` datatype, for example:

```haskell
 data ArithmeticError = DivideByZero
                      | NotDivisible
                      -- could add more cases here
                      deriving Show

safe_divide :: Int -> Int -> Either ArithmeticError Int
safe_divide _ 0                  = Left DivideByZero
safe_divide i j | i `mod` j /= 0 = Left NotDivisible
safe_divide i j                  = Right (i `div` j)
```

#### Use existential types for your own exceptions

Once you add a new constructor to ArithmeticError, you may have to update
a lot of pattern matches. So you should use an existential type - which
is a GHC extension - for error values, so that your code is more extensible.

An _existential type_ is a type with a type variable on the right hand side
which has no corresponding variable on the left, just a type restriction.
This is what `Control.Exception` uses for its `SomeException`:

```haskell
class (Show e, Typeable e) => Exception e where
    toException :: e -> SomeException
    fromException :: SomeException -> Maybe e

data SomeException = forall a. Exception a => SomeException a
```

#### Hand-rolling an exception monad based on Either

Compare: `a -> m b` - how monadic functions should look like

to: `a -> (Either e) b` - monadic error-handling functions

We could write this:

```haskell
instance Monad (Either e) where
  (>>=) :: Either e a -> (a -> Either e b) -> Either e b
  Left  err >>= f = Left err
  Right val >>= f = f val

  return :: a -> Either e a
  return x = Right x

-- and therefore

g' :: Int -> Int -> Int -> Either ArithmeticError Int
g' i j k = do q1 <- i `safe_divide` k
              q2 <- j `safe_divide` k
              return (q1 + q2)
```

#### typeclass MonadError in Control.Monad.Error

```haskell
class Monad m => MonadError e m | m -> e where
  throwError :: e -> m a
  catchError :: m a -> (e -> m a) -> m a
```

MonadError defines handling of a particular kind of error, and a particular
error-handling monad. Eg. error could be `ArithmeticError`, and monad could
be `Either ArithmeticError`. This means you can define custom throw/catch
functions with special behaviours for a particular monad through an instance
of MonadError for that monad. 

`| m -> e` - is a _functional dependency_, monad `m` determines error type `e`
(which in practice means compiler treats the first instance of MonadError
for a specific monad as canon, and will not allow a different e.)

```haskell
instance MonadError ArithmeticError (Either ArithmeticError) where
  throwError = Left

  catchError (Left x) handler = handler x  -- handle error
  catchError mval     _       = mval       -- pass through

data ArithmeticError = DivideByZero
                     | NotDivisible Int Int -- save the indivisible values
                     deriving Show

-- by definition of MonadError we know that m must be a monad, so no need
-- to add that to function constraints
safe_divide :: MonadError ArithmeticError m => Int -> Int -> m Int
safe_divide _ 0                  = throwError DivideByZero
safe_divide i j | i `mod` j /= 0 = throwError (NotDivisible i j)
safe_divide i j                  = return (i `div` j)

some_fun :: Int -> Int -> Either ArithmeticError Int
some_fun i j = do k <- i `safe_divide` j
                  l <- return (i + j)
                  return (k + 2 * l)
               `catchError` \e ->
                 case e of
                   DivideByZero     -> throwError DivideByZero
                   NotDivisible i j -> return (i `div` j)
```

#### typeclass Error in Control.Monad.Error

There actually already exists definitions for Either-based monad error handling
in `Control.Monad.Error`:

```haskell
class Error a where
  noMsg :: a
  strMsg :: String -> a

  -- default definitions:
  noMsg = strMsg ""
  strMsg _ = noMsg

instance (Error e) => Monad (Either e) where
  -- ...
  fail msg = Left (strMsg msg)

instance (Error e) => MonadError e (Either e) where
  -- ...
```

and so we can convert the above code to:

```haskell
data ArithmeticError = DivideByZero
                     | NotDivisible Int Int
                     | OtherError String -- for Error instance
                     deriving Show

-- required to handle `fail`, see Monad definition above
instance Error ArithmeticError where
  strMsg = OtherError
```

and then you don't have to derive your own `MonadError`.

You will need to enable these:

`{-# LANGUAGE FlexibleContexts, MultiParamTypeClasses #-}`

### State using monads: `Control.Monad.State`

In GHC you can find it all in `Control.Monad.State`. Below, I will
take notes on how you could handroll one.

_TODO: lazy and strict state monads?_

How do you adjust function with a signature `(a, s) -> (b, s)` to a monad-compatible
form `a -> m b`? 

1. turn it into a single-argument function `a -> (s -> (b, s))`
2. define a type for the return value (here: called `State`)
3. define a `Monad` instance with a single-argument constructor (`State` has
    two arguments, so we must partially apply it: `instance Monad (State s)`)

#### `State` type

For the purpose of hand-rolling we can use `data State s b = State (s -> (b, s))`

(The actual `State` type is a bit different:
`newtype State s a = State { runState :: s -> (s, a) }`)

#### `State` monads

```haskell
instance Monad (State s) where
  return x = State (\st -> (x, st))

  mv >>= g = State (\st ->
             -- extract transformation function
             let (State ff) = mv
                 -- transform state; return new state and extracted value
                 (y, st')   = ff st
                 -- pass value to g; receive another transformer
                 (State gg) = g y 
             -- transform state; return new state and extracted value
             in gg st')
```

(note that there are two `State` entities defined: type constructor and
value constructor, and they are in two different namespaces.)

In order to actually get the value out of the the whole chain of lambdas,
you have to create an initial state and execute the whole chain with the utility
functions below (which are defined in `Control.Monad.State`)

```haskell
-- get both value and state after state transition
runState :: State s a -> s -> (a, s)
runState (State f) init_st = f init_st

-- get the value after state transition
evalState :: State s a -> s -> a
evalState mv init_st = fst (runState mv init_st)

-- get the state after state transition
execState :: State s a -> s -> s
execState mv init_st = snd (runState mv init_st)
```

#### `MonadState` type class

Makes it easier to extract values from `State` and update it.

```haskell
class (Monad m) => MonadState s m | m -> s where
  get :: m s
  put :: s -> m ()

instance MonadState s (State s) where
  get = State (\s -> (s, s))
  put s = State (\_ -> ((), s))

modify :: (MonadState s m) => (s -> s) -> m ()
modify f =
  do st <- get
     put (f st)
```

Then you can write code like:

```haskell
type GCDState = (Int, Int)

gcd :: State GCDState Int
gcd = do (x, y) <- get
         case compare x y of
           EQ -> return x
           LT -> do put (y, x)
                    gcd
           GT -> do put (y, x - y)
                    gcd
```

And create a while loop for shits and giggles:

```haskell
while :: (s -> Bool) -> State s () -> State s ()
while test body =
  do st <- get
     if (test st)
       then do modify (execState body)
               while test body
       else return ()
```

## Assorted Haskell gotchas

Monads impose the sequencing of operations on their computations.

> For normal Haskell expressions like (i `div` k) + (j `div` k) the order
> of operations is not specified; Haskell is a lazy language and can evaluate
> this expression in whatever order it wants

How you define a type impacts efficiency

> `newtype` is used with single-constructor classes because the compiler
> can generate more efficient code with a declaration than with
> a `data` declaration

newtype is only usable if you have exactly one constructor
with exactly one field inside it